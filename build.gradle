buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

// Load keystore
def keystorePropertiesFile = rootProject.file("../../../vendor/keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// Define commonly used versions in a single place
ext {
    // Sdk and tools
    compileSdkVersion = 28
    buildToolsVersion = '28.0.2'

    minSdkVersion = 23
    targetSdkVersion = 28

    // App dependencies
    supportLibraryVersion = '27.1.1'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.katsuna.gallery"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        project.ext.set("archivesBaseName", "KatsunaGallery-" + defaultConfig.versionName)

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled false

        ndk {
            abiFilters "armeabi-v7a"
            //abiFilters ABI_FILTERS
        }
    }

    signingConfigs {
        katsunaConfig {
            storeFile rootProject.file("../../../vendor/keys/katsuna.keystore")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            signingConfig signingConfigs.katsunaConfig

            lintOptions {
                checkReleaseBuilds false
            }
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src','src_pd']
            jni.srcDirs = ['jni','jni_jpegstream']
            renderscript.srcDirs = ['rs']
            manifest.srcFile 'AndroidManifest.xml'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'jni/Android.mk'
            path 'jni_jpegstream/Android.mk'
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-fragment:$supportLibraryVersion"
    implementation "com.android.support:support-compat:$supportLibraryVersion"
    implementation "com.android.support:support-core-ui:$supportLibraryVersion"
    implementation "com.android.support:support-v13:$supportLibraryVersion"
    implementation project(':gallerycommon')
    implementation project(':commons')
}